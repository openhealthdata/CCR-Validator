/*
 * Copyright 2010 OpenHealthData, Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.

 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
#created on: Apr 29, 2008
package org.astm.ccr.rules.meaningfuluse

#list any import classes here.
import org.openhealthdata.validation.result.ValidationResultManager;
import org.openhealthdata.validation.result.TestResultType;
import org.openhealthdata.validation.result.ErrorType;

import org.astm.ccr.AlertType;
import org.astm.ccr.Reaction;
import org.astm.ccr.Agent;
import org.astm.ccr.Agent.*;
import org.astm.ccr.CodedDescriptionType;
import org.astm.ccr.CodeType;
import org.astm.ccr.StructuredProductType;
import org.astm.ccr.StructuredProductType.Product;
import java.util.List;


#declare any global variables here
global ValidationResultManager val_result;


// RULE that Allergies need to be coded with UNII
// TODO add differiation of non-medication allergies, which do not use agent
rule "Medication Allergies MUST be encoded with UNII - Description"
@title ("Medication Allergies MUST be encoded in UNII")
@testid ("8bf19d27-5b71-4786-8330-64b7357ad489")
@description ("This test checks for at least one code with a coding system of either UNII or unii for an allergy.  An UNII Code must be used for medication alleries")
@source ("RIN 0991-AB58")
@profile ("RIN 0991-AB58")
@author ("openhealthdata")
	when
		$al: AlertType( $ty:type, $des:description, agent == null)
		CodedDescriptionType (this == $ty, text == "Allergy")
		CodedDescriptionType(this == $des, $c:code)
		not (CodeType( codingSystem in ("UNII", "unii") ) from $c)
	then
	    val_result
	    .addFailed( drools.getRule() )
	    .addWarning( "Alert Allergy [" + $al.getCCRDataObjectID() + "] does not appear to be " +
	                 "coded in UNII which is required if it is a medication allergy." );
end
	
rule "Medication Allergies MUST be encoded with UNII - Agent"
@title ("Medication Allergies MUST be encoded in UNII")
@testid ("8bf19d27-5b71-4786-8330-64b7357ad480")
@description ("This test checks for at least one code with a coding system of either UNII or unii for the <ProductName> in <Agent>.  An UNII Code must be used for medication alleries")
@source ("RIN 0991-AB58")
@profile ("RIN 0991-AB58")
@author ("openhealthdata")
	when
		$al: AlertType( $ty:type, $ag:agent)
		CodedDescriptionType (this == $ty, text == "Allergy")
		Agent( this == $ag, $agprod:products)
		Agent.Products (this == $agprod, $prod:product)
		// TODO finish this
		StructuredProductType($sptprod:product ) from $prod
		Product( this == $sptprod, $pn:productName )
		CodedDescriptionType( this == $pn, $c:code  )
		not (CodeType( codingSystem in ("UNII", "unii") ) from $c)
	then
	    val_result
	    .addFailed( drools.getRule() )
	    .addFatal( "Alert Allergy [" + $al.getCCRDataObjectID() + "] does not appear to be " +
	               "coded in UNII which is required if it is a medication allergy." );
end
