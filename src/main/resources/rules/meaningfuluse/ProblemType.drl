/*
 * Copyright 2010 OpenHealthData, Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.

 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
 #created on: Jan 14, 2010
package org.astm.ccr.rules.meaningfuluse

#list any import classes here.
import org.openhealthdata.validation.result.ValidationResultManager;
import org.openhealthdata.validation.result.TestResultType;
import org.openhealthdata.validation.result.ErrorType;

import java.util.List;
import org.astm.ccr.ProblemType;
import org.astm.ccr.CodedDescriptionType;
import org.astm.ccr.CodeType;


#declare any global variables here
global ValidationResultManager val_result;



rule "MU - Problem description MUST be coded in either ICD9-CM, or SNOMED"
@title ("Problem description must be coded in either ICD9-CM, or SNOMED")
@testid ("f55b0a86-8b56-4468-8de3-0246fe186fa6")
@description ("Checks to see if Problem description has at least one code with a CodingSystem of either ICD9-CM, SNOMED, or SNOMED-CT. As referenced in 42CFR-Part 162.1002(a)(1)")
@source ("RIN 0991-AB58 Sec 170.205(2)(i)(A) and (B)")
@profile ("RIN 0991-AB58")
@author ("openhealthdata")
	when
		$p: ProblemType( $d:description )
		CodedDescriptionType( this == $d, $c:code )
		not (CodeType( codingSystem in ("ICD9-CM", "ICD9CM", "SNOMED", "SNOMED-CT", "SNOMEDCT") ) from $c)
	then
	    val_result
	    .addFailed( drools.getRule() )
	    .addFatal( "Problem [" + $p.getCCRDataObjectID() + "] does not appear to be " + 
	               "coded in either ICD9-CM, ICD-10, or SNOMED" );
end

rule "MU - No Problem Found in CCR"
@title ("Problems must be supported by the certified EHR technology but are not required for each CCR")
@testid ("61d25f67-2197-4bad-b688-f6d0ccb37f47")
@description ("Checks to see if Problems are provided in the CCR ")
@source ("RIN 0991-AB58 Sec 170.205(2)(i)")
@profile ("RIN 0991-AB58")
@author ("openhealthdata")
	when
		not $p: ProblemType()
	then
		val_result
	    .addPassed( drools.getRule() )
	    .addWarning( "No Problems were found therefore cannot evaluate compliance with " +
	                 "Problem List criteria" );
end
